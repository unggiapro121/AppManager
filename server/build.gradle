// SPDX-License-Identifier: GPL-3.0-or-later

plugins {
    id('com.android.library')
}

android {
    namespace 'io.github.muntashirakon.AppManager.server'
    compileSdk compile_sdk

    defaultConfig {
        minSdk min_sdk
        targetSdk target_sdk
    }

    compileOptions {
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    libraryVariants.configureEach { variant ->
        variant.assembleProvider.configure() {
            it.doLast {
                makeJars(variant.buildType.name)
            }
        }
    }
}

dependencies {
    compileOnly project(path: ':hiddenapi')

    api project(path: ':libserver')

    implementation "androidx.annotation:annotation:${annotation_version}"
}

def makeJars(String buildType) {
    println("Build type ${buildType}")

    if (!isModified()) {
        println("Files aren't modified, skip creating jar files.")
        return
    }

    String debugClassDir = project.rootDir.absolutePath + '/server/build/intermediates/javac/' + buildType + '/classes'
    String libserverClassDir = project.rootDir.absolutePath + '/libserver/build/intermediates/javac/' + buildType + '/classes'
    def amJar = project.rootDir.absolutePath + '/app/src/main/assets/am.jar'
    def mainJar = project.rootDir.absolutePath + '/app/src/main/assets/main.jar'

    println("Build success, creating am.jar...")
    def buildArgs = ['--release', '--output', amJar]
    buildArgs.addAll(getClassFiles(libserverClassDir, ["io/github/muntashirakon/AppManager/server/common/"]))
    buildArgs.addAll(getClassFiles(debugClassDir, ["io/github/muntashirakon/AppManager/server/"]))
    buildArgs.addAll(['--lib', "${android.sdkDirectory.path}/platforms/android-${target_sdk}/android.jar"])
    println(buildArgs.toString())
    exec {
        workingDir = project.rootDir
        executable = file("${android.sdkDirectory.path}/build-tools/${android.buildToolsVersion}/d8")
        args = buildArgs
    }
    println("Created ${amJar}")
    println("Creating main.jar...")
    buildArgs = ['--release', '--output', mainJar]
    buildArgs.add("${debugClassDir}/io/github/muntashirakon/AppManager/server/RootServiceMain.class")
    buildArgs.add("${libserverClassDir}/io/github/muntashirakon/AppManager/server/common/ServerUtils.class")
    buildArgs.addAll(['--lib', "${android.sdkDirectory.path}/platforms/android-${target_sdk}/android.jar"])
    exec {
        workingDir = project.rootDir
        executable = file("${android.sdkDirectory.path}/build-tools/${android.buildToolsVersion}/d8")
        args = buildArgs
    }
    println("Created ${mainJar}")
}

def isModified() {
    def stdout = new ByteArrayOutputStream()
    exec {
        workingDir = project.rootDir
        executable = "${project.rootDir}/scripts/check_am_server.sh"
        standardOutput = stdout
    }
    return stdout.toString().trim() == "M"
}

def getClassFiles(String path, List<String> pkgPath) {
    def ret = []
    pkgPath.forEach {
        it ->
            def file = new File(path, it)
            loadClsFiles(file, ret)
    }
    return ret
}

def loadClsFiles(File f, List paths) {
    File[] files = f.listFiles()
    if (files != null) {
        files.each {
            file ->
                if (file.file) {
                    if (file.name.endsWith(".class") && !file.name.startsWith("IPCMain")) {
                        paths.add(file.path)
                    }
                } else {
                    loadClsFiles(file, paths)
                }
        }
    }
}

tasks.register('cleanupJar') {
    doLast {
        file("${rootProject.projectDir.absolutePath}/app/src/main/assets/am.jar").delete()
        file("${rootProject.projectDir.absolutePath}/app/src/main/assets/main.jar").delete()
    }
}

clean.dependsOn cleanupJar
